# Example HCOM script, takes one argument, runs a simulation arg number of times
# $Id: simLoop.hcom 4939 2013-01-28 10:52:04Z petno25 $


#Simulate worst points and evaluate objective function
define evalworst
    opt worst
    evalId = ans
    call eval
enddefine


#Simulate to get initial objective values
define evalall
    i=0
    while (i<npoints)
        evalId = i
        call eval
        i=i+1
    repeat
enddefine

define eval
    chpa GainP.k::Value par(evalId,0)
    chpa GainI.k::Value par(evalId,1)
    sim
    call obj    
enddefine


#Objective function
define obj
    #Minimize absolute average difference
    error = Mass.P2.x-Step.out.val
    abs error
    obj1 = aver(error)

    #Minimize overshoot
    maxVal = max(Mass.P2.x)
    if(maxVal > 0.7)
        obj2 = maxVal-0.7
    else
        obj2 = 0
    endif

    totalObj = w1*r1*exp(e1)*obj1+w2*r2*exp(e2)*obj2
    opt setobj evalId totalObj
enddefine



npoints=6
nparams=2
maxevals=500
alpha=1.3
rfak=0.3
gamma=0.0
functol=0.0001
partol=0.001
w1=3
r1=1
e1=0
w2=1
r2=1
e2=0

sim
chpv Step.out.val Mass.P2.x

opt init complex double

#Set min and max values for parameters
opt setparminmax 0 0.00001 0.05
opt setparminmax 1 0.000001 0.0005



opt run


#ddt - nya variabler 
#lp1

    
